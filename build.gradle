plugins {
    id 'java-library'
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower-mini' version '1.2.1'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id "me.champeau.jmh" version "0.6.6"
}

allprojects {
    archivesBaseName = "${project.archives_base_name}-mc${project.minecraft_version}"
    group = project.maven_group
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    version = project.mod_version + "." + getVersionSuffix()

    repositories {
        maven { url 'https://jitpack.io' }
    }

    if (plugins.hasPlugin("fabric-loom")) {
        loom {
            shareRemapCaches = true
        }
    }
    afterEvaluate {
        if (plugins.hasPlugin("fabric-loom")) {
            migrateMappings.configure {
                outputDir = project.file("src/main/java")
            }
        }
    }
}

afterEvaluate {
    logger.lifecycle("Version String: ${version}")
}

loom {
    accessWidenerPath = file("src/main/resources/c2me.accesswidener")
}

configurations {
    api.extendsFrom includeApi
    implementation.extendsFrom shadowImpl
    runtimeOnly.extendsFrom shadowRuntime
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    api "net.fabricmc:yarn:${project.yarn_mappings}:v2" // loom bug?
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    includeApi project(":libs")
    include project(":libs")
    shadowImpl "com.ibm.async:asyncutil:${async_util_version}"
    shadowImpl "com.electronwill.night-config:toml:${night_config_version}"
    shadowImpl "org.threadly:threadly:${threadly_version}"

    afterEvaluate {
        project(":compatibility").subprojects.each {
            shadowRuntime(it) {
                transitive false
            }
        }
    }

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.

    // jmh
    afterEvaluate {
        jmh project.sourceSets.main.compileClasspath
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    from "LICENSE"
}

shadowJar {
    archiveClassifier = "all-dev"
    configurations = [ project.configurations.shadowImpl, project.configurations.shadowRuntime ]
    from "LICENSE"
}

//noinspection UnnecessaryQualifiedReference
task("remapShadowJar", type: net.fabricmc.loom.task.RemapJarTask, dependsOn: shadowJar) {
    input = shadowJar.archiveFile
    archiveFileName = shadowJar.archiveFileName.get().replaceAll("-dev\\.jar\$", ".jar")
    addNestedDependencies = true
    remapAccessWidener = true
    project(":libs").afterEvaluate {
        dependsOn(project(":libs").assemble)
    }
}

assemble.dependsOn(remapShadowJar)

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

String getVersionSuffix() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--dirty', '--broken'
        standardOutput = stdout
    }
    stdout = stdout.toString().strip()
    def suffix = ""
    if (stdout.endsWith("-dirty")) {
        stdout = stdout.substring(0, stdout.length() - "-dirty".length())
        suffix = "-dirty"
    }
    if (stdout.endsWith("-broken")) {
        stdout = stdout.substring(0, stdout.length() - "-broken".length())
        suffix = "-broken"
    }
    if (stdout.indexOf('-') < 0) {
        return "0" + suffix;
    }
    def split = stdout.split('-')
    return split[split.length - 2] + suffix
}

shadowJar {
//    minimize()
}

jmh {
//    includes = ['some regular expression'] // include pattern (regular expression) for benchmarks to be executed
//    excludes = ['some regular expression'] // exclude pattern (regular expression) for benchmarks to be executed
    iterations = 5 // Number of measurement iterations to do.
    benchmarkMode = ['avgt'] // Benchmark mode. Available modes are: [Throughput/thrpt, AverageTime/avgt, SampleTime/sample, SingleShotTime/ss, All/all]
    batchSize = 1 // Batch size: number of benchmark method calls per operation. (some benchmark modes can ignore this setting)
    fork = 2 // How many times to forks a single benchmark. Use 0 to disable forking altogether
    failOnError = false // Should JMH fail immediately if any benchmark had experienced the unrecoverable error?
//    forceGC = false // Should JMH force GC between iterations?
//    jvm = 'myjvm' // Custom JVM to use when forking.
//    jvmArgs = ['-XX:+UnlockDiagnosticVMOptions', '-XX:+LogCompilation', '-XX:+PrintAssembly', '-XX:+DebugNonSafepoints']
//    jvmArgsAppend = ['Custom JVM args to use when forking (append these)']
//    jvmArgsPrepend =[ 'Custom JVM args to use when forking (prepend these)']
//    humanOutputFile = project.file("${project.buildDir}/reports/jmh/human.txt") // human-readable output file
    resultsFile = project.file("${project.buildDir}/reports/jmh/results.txt") // results file
//    operationsPerInvocation = 10 // Operations per invocation.
//    benchmarkParameters =  [:] // Benchmark parameters.
    profilers = ['perf'] // Use profilers to collect additional data. Supported profilers: [cl, comp, gc, stack, perf, perfnorm, perfasm, xperf, xperfasm, hs_cl, hs_comp, hs_gc, hs_rt, hs_thr, async]
    timeOnIteration = '2s' // Time to spend at each measurement iteration.
    resultFormat = 'CSV' // Result format type (one of CSV, JSON, NONE, SCSV, TEXT)
//    synchronizeIterations = true // Synchronize iterations?
    threads = 1 // Number of worker threads to run with.
//    threadGroups = [2,3,4] //Override thread group distribution for asymmetric benchmarks.
//    timeout = '1s' // Timeout for benchmark iteration.
    timeUnit = 'ns' // Output time unit. Available time units are: [m, s, ms, us, ns].
    verbosity = 'NORMAL' // Verbosity mode. Available modes are: [SILENT, NORMAL, EXTRA]
    warmup = '1s' // Time to spend at each warmup iteration.
    warmupBatchSize = 1 // Warmup batch size: number of benchmark method calls per operation.
//    warmupForks = 0 // How many warmup forks to make for a single benchmark. 0 to disable warmup forks.
    warmupIterations = 16 // Number of warmup iterations to do.
    warmupMode = 'INDI' // Warmup mode for warming up selected benchmarks. Warmup modes are: [INDI, BULK, BULK_INDI].
    warmupBenchmarks = ['.*Warmup'] // Warmup benchmarks to include in the run in addition to already selected. JMH will not measure these benchmarks, but only use them for the warmup.

    zip64 = true // Use ZIP64 format for bigger archives
    jmhVersion = '1.34' // Specifies JMH version
    includeTests = true // Allows to include test sources into generate JMH jar, i.e. use it when benchmarks depend on the test classes.
    duplicateClassesStrategy = DuplicatesStrategy.FAIL // Strategy to apply when encountring duplicate classes during creation of the fat jar (i.e. while executing jmhJar task)
}
